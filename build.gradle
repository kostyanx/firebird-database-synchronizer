buildscript {
    ext {
        //noinspection GroovyAssignabilityCheck
        osgi_platform = 'gtk.linux.x86_64'
        //noinspection GroovyAssignabilityCheck
        swt_version = '3.113.0'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70-eap-208'
    id 'org.jetbrains.kotlin.kapt' version '1.3.70-eap-208'
    id 'java-library-distribution'
    id 'idea'
}

group 'ru.catcab'
version '1.0.0'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 13
targetCompatibility = 13

compileJava.options.encoding = 'UTF-8'


repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://catcab.ru:444/repository/maven-releases/"
    }
    maven {
        url "https://dl.bintray.com/kotlin/kotlin-dev/"
    }
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.$osgi_platform:$swt_version")
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ru.catcab:catcab-common-dbproxy:1.1.0'
    implementation 'org.eclipse.platform:org.eclipse.swt.gtk.linux.x86_64:3.113.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.13.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.0'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.0'
    implementation 'ru.catcab:catcab-common-dagger:1.0.3'
    implementation 'ru.catcab:catcab-common-utils:1.3.0'
    implementation 'com.zaxxer:HikariCP:3.4.2'
    implementation 'org.firebirdsql.jdbc:jaybird:4.0.0.java11-beta-2'
    implementation 'com.google.dagger:dagger:2.26'

    kapt 'com.google.dagger:dagger-compiler:2.26'
}

compileKotlin {
    kotlinOptions.jvmTarget = "12"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "12"
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.catcab.tool.database.synchronizer.DatabaseSynchronizerMainKt',
                'Class-Path': configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
    }
}
